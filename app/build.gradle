apply plugin: 'com.android.application'
Properties prop = new Properties()
prop.load(new FileInputStream(file("../signing.properties")))

def versionTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}
android {
    compileSdkVersion rootProject.ext.android["compileSdkVersion"]
    defaultConfig {
        applicationId "lucidastar.com.packgeapk"
        minSdkVersion rootProject.ext.android["minSdkVersion"]
        targetSdkVersion rootProject.ext.android["targetSdkVersion"]
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        flavorDimensions "versionCode"
    }
    signingConfigs {
        release {
            keyAlias prop['KEY_ALIAS']
            keyPassword prop['KEY_PASSWORD']
            storeFile file(prop['STORE_FILE'])
            storePassword prop['STORE_PASSWORD']
        }
        debug {
            // 显示Log
            keyAlias prop['KEY_ALIAS']
            keyPassword prop['KEY_PASSWORD']
            storeFile file(prop['STORE_FILE'])
            storePassword prop['STORE_PASSWORD']
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        debug {
            minifyEnabled false
            useProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testProguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguardTest-rules.pro'
        }
        release {
            minifyEnabled true
            useProguard true
            // 不显示Log
            buildConfigField "boolean", "LOG_DEBUG", "false"
            //Zipalign优化
            zipAlignEnabled true
            // 移除无用的resource文件
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            testProguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguardTest-rules.pro'
            signingConfig signingConfigs.release

            applicationVariants.all { variant ->
                variant.outputs.all { output ->
                    //==================================================================================
                    //apk的输出路径  也可以注释掉这段  会默认输出路径
                    variant.getPackageApplication().outputDirectory = new File(prop['APK_OUT_PATH'] + "/apk/release")
                    //==================================================================================
                    def outputFile = output.outputFile
                    if (outputFile != null && outputFile.name.endsWith('.apk')) {
                        // 输出apk名称为com_biniusports_app_bnsports_v1.0.0_20171205_baidu.apk
                        //工程的名称  版本名 时间  平台名称
                        def fileName = "${project.name}_v${defaultConfig.versionName}_${versionTime()}_${variant.productFlavors[0].name}.apk"
                        outputFileName = fileName
                    }
                }
            }
        }

    }

    productFlavors {
        wandoujia {}
        baidu {}
        c360 {}
        uc {}
        productFlavors.all { flavor ->
            flavor.manifestPlaceholders = [UMENG_CHANNEL_VALUE: name]
        }
    }

    tasks.whenTaskAdded { task ->
        if (task.name.contains("lint")
                || task.name.equals("clean")
                || task.name.contains("mockableAndroidJar")
                || task.name.contains("UnitTest")
                || task.name.contains("AndroidTest")
        ) {
            task.enabled = false
        }
    }

}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation rootProject.ext.dependencies["support-appcompatV7"]
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
    implementation 'com.umeng.sdk:common:1.5.0'
    implementation 'com.umeng.sdk:analytics:7.5.0'
    implementation 'com.android.support:recyclerview-v7:26.1.0'
}
